; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{0D60AE44-747E-4455-9421-7C6E6BCF4F5A}
AppName=Sauce RC
AppVerName=Sauce RC 1.1
AppPublisher=Sauce Labs Inc
AppPublisherURL=http://saucelabs.com/
AppSupportURL=http://saucelabs.com/
AppUpdatesURL=http://saucelabs.com/
DefaultDirName={pf}\Sauce Labs\Sauce RC
DefaultGroupName=Sauce RC
OutputBaseFilename=Sauce-RC-1.1.build.this-will-be-changed-by-make
Compression=lzma
SolidCompression=yes
WizardImageFile=images\innosetup_image_big.bmp
WizardSmallImageFile=images\innosetup_image_small.bmp
LicenseFile=LICENSE.txt

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: checkedonce
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: checkedonce

[Files]
Source: dist\*; DestDir: {app}; Flags: ignoreversion recursesubdirs;
Source: images\selenium-rc-logo.ico; DestDir: {app}; Flags: ignoreversion
Source: images\sauce_logo.ico; DestDir: {app}; Flags: ignoreversion
Source: images\help.ico; DestDir: {app}; Flags: ignoreversion
Source: vcredist_x86.exe; DestDir: {tmp}; Flags: ignoreversion

[Icons]
Name: {group}\Sauce RC; Filename: {app}\saucerc.exe; Flags: dontcloseonexit; IconFilename: {app}\sauce_logo.ico
Name: {group}\{cm:ProgramOnTheWeb,Selenium}; Filename: "http://www.seleniumhq.org/"; IconFilename: {app}\selenium-rc-logo.ico
Name: {group}\{cm:ProgramOnTheWeb,Sauce Labs}; Filename: "http://saucelabs.com"; IconFilename: {app}\sauce_logo.ico
Name: {group}\{cm:UninstallProgram,Sauce RC}; Filename: {uninstallexe}
Name: {userdesktop}\Sauce RC; Filename: {app}\saucerc.exe; Flags: dontcloseonexit; IconFilename: {app}\sauce_logo.ico; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\Sauce RC"; Filename: {app}\saucerc.exe; Flags: dontcloseonexit; IconFilename: {app}\sauce_logo.ico; Tasks: quicklaunchicon
Name: {group}\Help; Filename: {app}\static\docs\index.html; IconFileName: {app}\help.ico

; Also in startup
Name: {userstartup}\Sauce RC; Filename: {app}\saucerc.exe; IconFilename: {app}\sauce_logo.ico

[Run]
Filename: {tmp}\vcredist_x86.exe; Parameters: "/q:a /c:""msiexec /i vcredist.msi /qn /l*v {tmp}\vcredist_x86.log"""
Filename: {app}\saucerc.exe; Description: "Run Sauce RC"; Flags: postinstall nowait skipifsilent

[UninstallRun]
FileName: {app}\uninstall_cleanup.exe

[UninstallDelete]
Type: files; Name: {app}\errors.log
Type: files; Name: {app}\saucerc.exe.log
Type: files; Name: {app}\sauceserver.exe.log
Type: files; Name: {app}\sauceproxy.exe.log

[Code]
procedure DecodeVersion (verstr: String; var verint: array of Integer);
var
  i,p: Integer; s: string;
begin
  // initialize array
  verint := [0,0,0,0];
  i := 0;
  while ((Length(verstr) > 0) and (i < 4)) do
  begin
    p := pos ('.', verstr);
    if p > 0 then
    begin
      if p = 1 then s:= '0' else s:= Copy (verstr, 1, p - 1);
      verint[i] := StrToInt(s);
      i := i + 1;
      verstr := Copy (verstr, p+1, Length(verstr));
    end
    else
    begin
      verint[i] := StrToInt (verstr);
      verstr := '';
    end;
  end;

end;

function CompareVersion (ver1, ver2: String) : Integer;
var
  verint1, verint2: array of Integer;
  i: integer;
begin

  SetArrayLength (verint1, 4);
  DecodeVersion (ver1, verint1);

  SetArrayLength (verint2, 4);
  DecodeVersion (ver2, verint2);

  Result := 0; i := 0;
  while ((Result = 0) and ( i < 4 )) do
  begin
    if verint1[i] > verint2[i] then
      Result := 1
    else
    if verint1[i] < verint2[i] then
      Result := -1
    else
      Result := 0;
    i := i + 1;
  end;

end;

(*
    FIXME: This check for Java does not work on 64 bit windows.
    The right and simple way is to do the following
    GetVersionNumbersString('C:\Windows\system32\java.exe', Version);
    GetPos('6', Version) = 1;

    However I (Miki) spent too much time on the damn if/then syntax and I really
    have much more pressing things right now. Documenting this so someone with more
    time (or me later) will fix that.

    When you fix this, please also http://saucelabs.com/forums/viewtopic.php?pid=353#p353
*)

function InitializeSetup(): Boolean;
var
  ErrorCode: Integer;
  JavaVer : String;
  Result1 : Boolean;
  PidFile: String;
begin
  RegQueryStringValue(HKLM, 'SOFTWARE\JavaSoft\Java Runtime Environment', 'CurrentVersion', JavaVer);
  Result := false;
  if Length( JavaVer ) > 0 then
  begin
    if CompareVersion(JavaVer,'1.6') >= 0 then
    begin
      Result := true;
    end;
  end;
  if Result = false then
  begin
    Result1 := MsgBox('Sauce RC requires Java Runtime Environment v1.6 or newer to start Selenium RC and it seems it is not present in your machine.' + #13 + #10 + 'Do you want to download and install JRE? (restart Sauce RC intallation later if Yes)',
                      mbConfirmation, MB_YESNO) = idYes;
    if Result1 = true then
      ShellExec('open',
                'http://www.java.com/en/download/manual.jsp#win',
                '','',SW_SHOWNORMAL,ewNoWait,ErrorCode)
    else
      Result := true;
  end;
  if Result = true then
  begin
    PidFile := GetEnv('APPDATA') + '\SauceRc\saucerc.pid';
    if FileExists(PidFile) = True then
    begin
     MsgBox('Sauce RC seems to be running. Please close it or remove the file ' + #13 + PidFile + ' and restart the installation process.', mbError, MB_OK);
     Result := false;
    end;
  end;
end;
